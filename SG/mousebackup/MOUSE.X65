;****************************************************************
;*								*
;*		mouse.x65					*
;*		Super NES Mouse System file			*
;*		March 11, 1992          			*
;*		(c) 1992 Nintendo of Ameica Inc.		*
;*								*
;****************************************************************
;
;****************************************************************
;****************************************************************
;*								*
;*		  Mouse Driver Routine (Ver 1.00)		*
;*						         	*
;****************************************************************
;****************************************************************
		db	'START OF MOUSE BIOS'	;do not delete

;================================================================
;		RAM Definition    
;================================================================

;reg0
;reg0l		ds	1	; Work registers
;reg0h		ds	1

;mouse_con	ds	1	;
;mouse_con0	ds	1	; Mouse connection port D0=4016
;mouse_con1	ds	1	; Mouse connection port D0=4017

;mouse_sp_set
;mouse_sp_set0	ds	1	; Mouse Speed Setting (joy1)
;mouse_sp_set1	ds	1	; Mouse Speed Setting (joy2)

;mouse_sp
;mouse_sp0	ds	1	; Mouse Speed (joy1)
;mouse_sp1	ds	1	; Mouse Speed (joy2)

;mouse_y0	ds	1	; Mouse Y-direction  (joy1)
;mouse_y1	ds	1	; Mouse Y-direction  (joy2)
;mouse_x0	ds	1	; Mouse X-direction  (joy1)
;mouse_x1	ds	1	; Mouse X-direction  (joy2)

;mouse_sw
;mouse_sw0	ds	1	; Mouse button turbo
;mouse_sw1	ds	1	; Mouse button turbo

;mouse_swt
;mouse_swt0	ds	1	; Mouse button trigger
;mouse_swt1	ds	1	; Mouse button trigger

;mouse_sb			; Previous Switch status
;mouse_sb0	ds	1
;mouse_sb1	ds	1

;cursol_x	ds	1	; Cursor X position    
;cursol_y	ds	1	; Cursor Y position    

;*******************************************************************
;*  ============================================================== *
;*        Mouse_read                                               *
;*  ============================================================== *
;*								   *
;*  If this routine is called every frame, then the mouse status   *
;*     will be set to the appropriate registers.                   *
;*                                                                 *
;* INPUT                                                           *
;*     None (Mouse key read automatically                          *
;*                                                                 *
;* OUTPUT                                                          *
;*     Connection status (mouse_con) D0=1 Mouse connected to Joy1  *
;*	                             D1=1 Mouse connected to Joy2  *
;*                                                                 *
;*     Switch (mouse_sw0,1)  D0=left switch turbo                  *
;*                           D1=right switch turbo                 *
;*                                                                 *
;*     Switch (mouse_swt0,1) D0=left switch trigger                *
;*                           D1=right switch trigger               *
;*                                                                 *
;*     Mouse movement (ball) value                                 *
;*            (mouse_x) D7=0 Positive Turn, D7=1 Negative Turn     *
;*			     D6-D0 X movement value                *
;*            (mouse_y) D7=0 Positive Turn, D7=1 Negative Turn     *
;*			     D6-D0 Y movement value                *
;*                                                                 *
;*                                                                 *
;******************************************************************* 

mouse_read

	php
	sep	#$30		
_10
	lda	$4212
	and	#%00000001
	bne	_10		; Automatic read okay?

	ldx	#$01		; Joy 2
	lda	$421a
	jsr	mouse_data

	lda	connect_st1
	beq	.20

	jsr	speed_change	
	stz	connect_st1

	plp
	rts
.20
	dex
	lda	$4218		; Joy 1    
	jsr	mouse_data

	lda	connect_st0
	beq	.30

	jsr	speed_change	
	stz	connect_st0
.30
	plp
	rts


mouse_data
	sta	reg0l		; (421a 4218 save to reg0)  
	and	#%00001111
	cmp	#$01		; Is mouse connected?        
	beq	_m10

	stz	mouse_con0,x	; No connection

	stz	mouse_x0,x
	stz	mouse_y0,x
	stz	mouse_sw0,x
	stz	mouse_swt0,x
	stz	mouse_sb0,x

	rts
_m10
	lda	mouse_con0,x	; When mouse is connected, speed will change
	bne	_m20		; Previous connection status
				;	(mouse.com judged by lower 1 bit)

	lda	#$01		; Connection check flag ON
	sta	mouse_con0,x
	sta	connect_st0,x
	rts

  
_m20
	ldy	#16		; Read 16-bit data
_m30
	lda	$4016,x
	lsr	a
	rol	mouse_x0,x
	rol	mouse_y0,x
	dey
	bne	_m30

	stz	mouse_sw0,x

	rol	reg0l
	rol	mouse_sw0,x
	rol	reg0l
	rol	mouse_sw0,x	; Switch Turbo  

	lda	mouse_sw0,x
	eor	mouse_sb0,x	; Get Switch trigger 
	bne	_m40

	stz	mouse_swt0,x

	rts
_m40
	lda	mouse_sw0,x
	sta	mouse_swt0,x
	sta	mouse_sb0,x

	rts


;*******************************************************************
;*  =============================================================  *
;*	Speed_change                                   		   *
;*  =============================================================  *
;*  Set speed to mouse_sp_set.  Give mouse port the value of x and *
;*	call this routine.					   *
;*  If this routine is called without setting mouse_sp_set, then   *
;*      previous speed will be assigned to current speed.          *
;*  Normally, mouse speed data will be saved to mouse_sp.	   *
;*  If mouse speed cannot be set properly, then error code will    *
;*  	be set to mouse_sp.					   *	
;*								   *
;* INPUT							   *
;*      X=connection port (X:0=joy1 1=joy2)                        *
;*      MOUSE_SP_SET0= JOY1 setting speed                          *
;*      MOUSE_SP_SET1= JOY2 setting speed                          * 
;* OUTPUT							   *
;*      MOUSE_SP0 = Joy1 Mouse speed                               *
;*	          (0=slow, 1=medium, 2=fast, $80=error code        *
;*      MOUSE_SP1 = Joy2 Mouse speed                               *
;*	          (0=slow, 1=medium, 2=fast, $80=error code        *
;*******************************************************************

speed_change
	php
	sep	#$30

	lda	mouse_con,x	
	beq	_s25

	lda	#$10		
	sta	reg0h		

.s10
	lda	#$01
	sta	$4016
	lda	$4016,x		; Speed change (1 step)
	stz	$4016

	lda	#$01		; Read Speed data   
	sta	$4016		; Shift register clear
	lda	#$00
	sta	$4016

	sta	mouse_sp0,x	; Speed register clear  

	ldy	#10		; Shift register read has no meaning
.s20
	lda	$4016,x		;
	dey
	bne	.s20

	lda	$4016,x		; Read Speed  
	lsr	a
	rol	mouse_sp0,x

	lda	$4016,x
	lsr	a
	rol	mouse_sp0,x
	lda	mouse_sp0,x

	cmp	mouse_sp_set0,x	; Set speed or not? 
	beq	_s30

	dec	reg0h		; For error check         
	bne	.s10

_s25
	lda	#$80		; Speed change error    
	sta	mouse_sp0,x
_s30
	plp
	rts

	db	'NINTENDO SHVC MOUSE BIOS Ver1.00' 	;do not delete
;
;            if user modifies program, then change to 
;            'MODIFIED FROM SHVC MOUSE BIOS Ver1.00'
;

	db	'END OF MOUSE BIOS'	    		;do not delete

