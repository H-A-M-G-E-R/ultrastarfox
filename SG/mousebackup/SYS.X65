;****************************************************************
;*								*
;*		sys.x65						*
;*		Super NES Mouse System file			*
;*		March 10, 1992 		 			*
;*		(c) 1992 Nintendo of America Inc.		*
;*								*
;****************************************************************
irq
	rti
;
;==================================================================
;		NMI Blanking       
;
nmi
	phd
	pha
	phx
	phy
	php

	ai8
	lda	nmiwt_f
	beq	_900		; NMI error?     

	jsr	oam_set		; OAM set    

	ldx	box_no
	jsr	box_write

	jsr	atr_write

	ldx	box_no2
	jsr	box_write
	ldx	box_no3
	jsr	box_write
	ldx	box_no4
	jsr	box_write
	ldx	box_no5
	jsr	box_write



_900
	lda	#$00
	sta	nmiwt_f		; Clear NMI wait flag

	plp
	ply
	plx
	pla
	pld

	rti
;
;=================================================================
;		NMI Wait 
;
	ai8
;
;
nmi_wt
	lda	#$01
	sta	nmiwt_f
_loop
	lda	nmiwt_f
	bne	_loop

	rts
;
;=================================================================
;		NMI on 
;
	ai8
;
;
nmi_on
	lda	r4200
	ora	#%10000000
	sta	r4200
	sta	$4200
	rts
;
;=================================================================
;		NMI off      
;
	ai8
;
;
nmi_off
	lda	r4200
	and	#%01111111
	sta	r4200
	sta	$4200
	rts
;
;=================================================================
;		Blanking On      
;
	ai8
;
;
blkn_on
	lda	r2100
	ora	#%10000000
	sta	r2100
	sta	$2100
	rts
;
;=================================================================
;		Blanking Off       
;
	ai8
;
;
blkn_off
	lda	r2100
	and	#%01111111
	sta	r2100
	sta	$2100
	rts
;
;=================================================================
;		RST28     
;
	ai8
;
;
rst28
	asl	a
	tay
	iny

	a16

	pla
	sta	reg0
	lda	(reg0),y
	sta	reg1

	a8

	jmp	(reg1)
;
;=================================================================
;		OAM Set      
;
;	Use DMA Channel 0        
;
;			[  IN : OAM_AD (2Byte) = OAM address    
;
	ai8
;
;
oam_set
	lda	#0
	sta	$4304		; A bus (bank)     
	sta	$4300		; DMA parameter    
	lda	#$04
	sta	$4301		; B bus  

	a16

;	lda	#obj_ram
;	sta	$4302		; A bus address   
;	lda	#$0220
;	sta	$4305		; byte number

	a8

	lda	#%00000001
	sta	$420b		; OBJ DMA transfer

	lda	oam_adh
	sta	$2103
	lda	oam_adl
	sta	$2102
oam_set_rts
	rts
;
;==================================================================
;		OBJ Ram Full Clear       
;
	ai8
;
;
obj_clr
	i16

	ldy	#$0200
	lda	#$ef		; H-POS = 80H
.10
	sta	obj_ram,y
	dey
	bpl	.10

	i8
	a16

	ldy	#$10
	lda	#$0000
.20
	sta	obj_ram2-2,y
	dey
	dey
	bpl	.20

	a8

	rts
;
;===================================================================
;		Ram Clear         
;
;********  SC1 Clear  ********
;
	ai8
;
;
sc1_clr
	lda	#%10000000	; Vram inc. by 1 after upper byte access
	sta	$2115		; Vram address increment setting

	ai16

	ldy	#$0000
	sty	$2116		; Vram address $0000  
	lda	#space
.10
	sta	$2118		; Vram data
	iny
	cpy	#$1000
	bne	.10

	ai8

	rts
;
;********  SC2 Clear  ********
;
	ai8
;
;
sc2_clr
	lda	#%10000000
	sta	$2115		

	ai16

	ldy	#$1000
	sty	$2116		
	lda	#space
.10
	sta	$2118		
	iny
	cpy	#$1800
	bne	.10

	ai8

	rts
;
;********  SC3 Clear  ********
;
	ai8
;
;
sc3_clr
	lda	#%10000000
	sta	$2115		

	ai16

	ldy	#$1400
	sty	$2116		
	lda	#space
.10
	sta	$2118		
	iny
	cpy	#$2000
	bne	.10

	ai8

	rts
;
;//////////////  END OF FILE  //////////////////////////
;
;
	end
